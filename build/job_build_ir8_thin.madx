option, warn,info;
system,"rm -rf temp"; system,"mkdir temp";
!system,"ln -fns /afs/cern.ch/eng/lhc/optics/runII/2015 db5";
system,"ln -fns ../ db5";
system,"ln -fns /afs/cern.ch/eng/lhc/optics/runII/opt_med med";

Option, -echo,-warn,-info;
call,file="db5/lhc_as-built.seq";
call,file="db5/toolkit/macro.madx";
call,file="db5/toolkit/optics_log.madx";
Option, -echo,warn,-info;

NRJ=6500;
Beam,particle=proton,energy=NRJ,sequence=lhcb1,bv= 1;
Beam,particle=proton,energy=NRJ,sequence=lhcb2,bv=-1;

call,file="db5/opt_inj_colltunes.madx";
exec,crossing_disable;
exec,check_ip(b1); exec,check_ip(b2);

call,file="db5/toolkit/slice.madx";
call,file="db5/Misc/mq_thin.madx";

! start


gen_optics(bbb,kkk): macro={
exec,crossing_disable;
jac_tol=1e-23;
call,file="db5/toolkit/rematch_ir8b12.madx";
jac_tol=1e-25;
call,file="db5/toolkit/rematch_xing_ir8.madx";
value,tarir8b12,tarir8_o8,tarir8_x8,tarir8_sep8;
if (tarir8b12+tarir8_o8+tarir8_x8+tarir8_sep8>1e-16){return;};
system,"rm IR8/ir8_bbb_kkk_thin.madx";
option,-echo, -info;assign, echo="IR8/ir8_bbb_kkk_thin.madx";
call,file="db5/toolkit/save_optics_ir8.madx";
option,-echo,-info;assign, echo=terminal;
system,"db5/toolkit/split_madx_lines IR8/ir8_bbb_kkk_thin.madx";
call,file="IR8/ir8_bbb_kkk_thin.madx";
};

jac_calls=10;

!High beta squeeze
system,"cp IR8/ir8_10mto30m.tfs ir8_10mto30m_new.tfs";
exec,read_opticstbl(ir8,highbetold,ir8_10mto30m_new.tfs);
exec,make_opticstbl_ir8(highbet);

bbb=1;nnn=1;
while(bbb<30000){
  on_x8_ref=100;on_sep8_ref=1;on_o8_ref=0.1; on_a8_ref=10;
  exec,load_optics(highbetold,$nnn);
  acbx_sep8=acbxv1.l8_on_sep8/y_on_sep8;
  acbx_x8=acbxh1.l8_on_x8/px_on_x8;
  value,acbx_sep8,acbx_x8;
  bbb=round(betxip8b1*1000);
  kkk=round(kqx.l8*1e5);
  value,bbb,kkk;
  exec,gen_optics($bbb,$kkk);
  exec,store_optics(highbet);
  write,table=highbet,file="IR8/ir8_10mto30m_thin.tfs";
  nnn=nnn+1;
};

!Low beta squeeze
system,"cp IR8/ir8_10mto3m.tfs ir8_10mto3m_new.tfs";
exec,read_opticstbl(ir8,lowbetold,ir8_10mto3m_new.tfs);
exec,make_opticstbl_ir8(lowbet);

bbb=10000;nnn=1;
while(bbb>3000){
  on_x8_ref=170;on_sep8_ref=2;on_o8_ref=0.1; on_a8_ref=40;
  exec,load_optics(lowbetold,$nnn);
  acbx_sep8=acbxv1.l8_on_sep8/y_on_sep8;
  acbx_x8=acbxh1.l8_on_x8/px_on_x8;
  value,acbx_sep8,acbx_x8;
  bbb=round(betxip8b1*1000);
  kkk=round(kqx.l8*1e5);
  value,bbb,kkk;
  exec,gen_optics($bbb,$kkk);
  exec,store_optics(lowbet);
  write,table=lowbet,file="IR8/ir8_10mto3m_thin.tfs";
  nnn=nnn+1;
};


