scale0=1;

limitMCBXH:=  63.5988e-6*scale0;
limitMCBXV:=  67.0164e-6*scale0;
limitMCBY :=  96.3000e-6*scale0;
limitMCB  :=  80.8000e-6*scale0;
limitMCBC :=  89.8700e-6*scale0;
limitMCBW :=  80.1400e-6*scale0;

on_sep5_aux=on_sep5;on_x5_aux=on_x5;on_o5_aux=on_o5;on_a5_aux=on_a5;

y_on_sep5=1e-3; px_on_x5=1e-6; x_on_o5=1e-3;
!ttt2:=(NRJ-450)/6050;
!acbx_x= 40.00e-6* (1- ttt2) + 10.00e-6 * ttt2;
!acbx_s= 35.00e-6* (1- ttt2) + 10.00e-6 * ttt2;

!acbx_x= 1.000e-6;
!acbx_s= 5.000e-6;



xip5b1 := x_on_o5   * on_o5;
xip5b2 := x_on_o5   * on_o5;
yip5b1 := y_on_sep5 * on_sep5;
yip5b2 :=-y_on_sep5 * on_sep5;
pxip5b1:= px_on_x5  * on_x5;
pxip5b2:=-px_on_x5  * on_x5;
pyip5b1 = 0;
pyip5b2 = 0;

acbxh1.l5:=acbxh1.l5_on_x5 * on_x5;
acbxh1.r5:=acbxh1.r5_on_x5 * on_x5;
acbxh2.l5:=acbxh2.l5_on_x5 * on_x5;
acbxh2.r5:=acbxh2.r5_on_x5 * on_x5;
acbxh3.l5:=acbxh3.l5_on_x5 * on_x5;
acbxh3.r5:=acbxh3.r5_on_x5 * on_x5;
acbyhs4.l5b1:=acbyhs4.l5b1_on_x5 * on_x5 + acbyhs4.l5b1_on_o5 * on_o5;
acbyhs4.l5b2:=acbyhs4.l5b2_on_x5 * on_x5 + acbyhs4.l5b2_on_o5 * on_o5;
acbyhs4.r5b1:=acbyhs4.r5b1_on_x5 * on_x5 + acbyhs4.r5b1_on_o5 * on_o5;
acbyhs4.r5b2:=acbyhs4.r5b2_on_x5 * on_x5 + acbyhs4.r5b2_on_o5 * on_o5;
acbyh4.l5b1=0;
acbyh4.r5b2=0;
acbch5.r5b1:=acbch5.r5b1_on_x5 * on_x5 + acbch5.r5b1_on_o5 * on_o5;
acbch5.l5b2:=acbch5.l5b2_on_x5 * on_x5 + acbch5.l5b2_on_o5 * on_o5;
acbch6.l5b1:=acbch6.l5b1_on_x5 * on_x5 + acbch6.l5b1_on_o5 * on_o5;
acbch6.r5b2:=acbch6.r5b2_on_x5 * on_x5 + acbch6.r5b2_on_o5 * on_o5;
acbch7.r5b1=0;
acbch7.l5b2=0;

acbxv1.l5:=acbxv1.l5_on_sep5 * on_sep5;
acbxv1.r5:=acbxv1.r5_on_sep5 * on_sep5;
acbxv2.l5:=acbxv2.l5_on_sep5 * on_sep5;
acbxv2.r5:=acbxv2.r5_on_sep5 * on_sep5;
acbxv3.l5:=acbxv3.l5_on_sep5 * on_sep5;
acbxv3.r5:=acbxv3.r5_on_sep5 * on_sep5;
acbyvs4.l5b1 :=acbyvs4.l5b1_on_sep5 * on_sep5;
acbyvs4.r5b1 :=acbyvs4.r5b1_on_sep5 * on_sep5;
acbyvs4.l5b2 :=acbyvs4.l5b2_on_sep5 * on_sep5;
acbyvs4.r5b2 :=acbyvs4.r5b2_on_sep5 * on_sep5;
acbyv4.r5b1=0;
acbyv4.l5b2=0;
acbcv5.l5b1  :=acbcv5.l5b1_on_sep5  * on_sep5;
acbcv5.r5b2  :=acbcv5.r5b2_on_sep5  * on_sep5;
acbcv6.r5b1  :=acbcv6.r5b1_on_sep5  * on_sep5;
acbcv6.l5b2  :=acbcv6.l5b2_on_sep5  * on_sep5;
acbch7.l5b2=0;
acbch7.r5b1=0;

! crossing
scale0=1/abs(on_x5_ref);
on_sep5=0;on_x5=1;on_o5=0;
acbxh1.l5_on_x5  = acbx_x5*px_on_x5;
acbxh1.r5_on_x5  =-acbx_x5*px_on_x5;
acbxh2.l5_on_x5  = acbx_x5*px_on_x5;
acbxh2.r5_on_x5  =-acbx_x5*px_on_x5;
acbxh3.l5_on_x5  = acbx_x5*px_on_x5;
acbxh3.r5_on_x5  =-acbx_x5*px_on_x5;
Use, period= LHCB1,RANGE=S.DS.L5.B1/E.DS.R5.B1;
Use, period= LHCB2,RANGE=S.DS.L5.B2/E.DS.R5.B2;
Match, sequence=lhcb1,lhcb2, beta0=bir5b1,bir5b2;
WEIGHT,     x = 1, px =  10, y = 1, py = 10;
constraint, sequence=lhcb1,range=ip5,       x = xip5b1, px = pxip5b1;
constraint, sequence=lhcb1,range=E.DS.R5.B1,x = 0.0, px = 0.0;
constraint, sequence=lhcb2,range=ip5,  x = xip5b2, px = pxip5b2;
constraint, sequence=lhcb2,range=E.DS.R5.B2,x = 0.0, px = 0.0;
vary,name=acbyhs4.l5b1_on_x5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch6.l5b1_on_x5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyhs4.r5b1_on_x5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch5.r5b1_on_x5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyhs4.l5b2_on_x5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch5.l5b2_on_x5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyhs4.r5b2_on_x5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch6.r5b2_on_x5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
!simplex,calls=100;
jacobian,calls=jac_calls, tolerance=1.E-30;
Endmatch;
tarir5_x5=tar;



! offset
scale0=1/abs(on_o5_ref);
acbxv1.l5_on_o5=  0;
acbxv1.r5_on_o5=  0;
acbxv2.l5_on_o5=  0;
acbxv2.r5_on_o5=  0;
acbxv3.l5_on_o5=  0;
acbxv3.r5_on_o5=  0;
if (x_on_o5==0){
acbyhs4.l5b1_on_o5=0;
acbch6.l5b1_on_o5=0;
acbyhs4.r5b1_on_o5=0;
acbch5.r5b1_on_o5=0;
acbyhs4.l5b2_on_o5=0;
acbch5.l5b2_on_o5=0;
acbyhs4.r5b2_on_o5=0;
acbch6.r5b2_on_o5=0;
tar=0;
} else {
on_sep5=0;on_x5=0;on_o5=1;
Use, period= LHCB1,RANGE=S.DS.L5.B1/E.DS.R5.B1;
Use, period= LHCB2,RANGE=S.DS.L5.B2/E.DS.R5.B2;
Match, sequence=lhcb1,lhcb2, beta0=bir5b1,bir5b2;
WEIGHT,     x = 1, px =  10, y = 1, py = 10;
constraint, sequence=lhcb1,range=ip5,       x = xip5b1, px = pxip5b1;
constraint, sequence=lhcb1,range=E.DS.R5.B1,x = 0.0, px = 0.0;
constraint, sequence=lhcb2,range=ip5,  x = xip5b2, px = pxip5b2;
constraint, sequence=lhcb2,range=E.DS.R5.B2,x = 0.0, px = 0.0;
vary,name=acbyhs4.l5b1_on_o5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch6.l5b1_on_o5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyhs4.r5b1_on_o5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch5.r5b1_on_o5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyhs4.l5b2_on_o5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch5.l5b2_on_o5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyhs4.r5b2_on_o5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbch6.r5b2_on_o5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
jacobian,calls=jac_calls, tolerance=1.E-30;
Endmatch;
};
tarir5_o5=tar;


! separation
scale0=1/abs(on_sep5_ref);
on_sep5=1;on_x5=0;on_o5=0;
acbxv1.l5_on_sep5=  acbx_sep5*y_on_sep5;
acbxv1.r5_on_sep5=  acbx_sep5*y_on_sep5;
acbxv2.l5_on_sep5=  acbx_sep5*y_on_sep5;
acbxv2.r5_on_sep5=  acbx_sep5*y_on_sep5;
acbxv3.l5_on_sep5=  acbx_sep5*y_on_sep5;
acbxv3.r5_on_sep5=  acbx_sep5*y_on_sep5;
Use, period= LHCB1,RANGE=S.DS.L5.B1/E.DS.R5.B1;
Use, period= LHCB2,RANGE=S.DS.L5.B2/E.DS.R5.B2;
Match, sequence=lhcb1,lhcb2, beta0=bir5b1,bir5b2;
WEIGHT,     x = 1, px =  10, y = 1, py = 10;
constraint, sequence=lhcb1,range=ip5,       y = yip5b1, py = pyip5b1;
constraint, sequence=lhcb1,range=E.DS.R5.B1,y = 0.0, py = 0.0;
constraint, sequence=lhcb2,range=ip5,  y = yip5b2, py = pyip5b2;
constraint, sequence=lhcb2,range=E.DS.R5.B2,y = 0.0, py = 0.0;
vary,name=acbcv5.l5b1_on_sep5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyvs4.l5b1_on_sep5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbyvs4.r5b1_on_sep5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbcv6.r5b1_on_sep5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyvs4.l5b2_on_sep5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbcv6.l5b2_on_sep5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
vary,name=acbyvs4.r5b2_on_sep5, step=1.0E-15,lower=-limitMCBY,upper=limitMCBY;
vary,name=acbcv5.r5b2_on_sep5 , step=1.0E-15,lower=-limitMCBC,upper=limitMCBC;
simplex,calls=100;
jacobian,calls=jac_calls, tolerance=1.E-30;
Endmatch;
tarir5_sep5=tar;

on_sep5=0;on_x5=0;on_o5=0;

on_sep5=1;on_x5=1;on_o5=1;on_a5=1;
use, period= lhcb1,range=s.ds.l5.b1/e.ds.r5.b1; twiss,beta0=bir5b1;
refpyip5b1=table(twiss,ip5,py);
refxip5b1=table(twiss,ip5, x);
refyip5b1=table(twiss,ip5, y);
refpxip5b1=table(twiss,ip5,px);
use, period= lhcb2,range=s.ds.l5.b2/e.ds.r5.b2; twiss,beta0=bir5b2;
refpyip5b2=table(twiss,ip5,py);
refxip5b2=table(twiss,ip5, x);
refyip5b2=table(twiss,ip5, y);
refpxip5b2=table(twiss,ip5,px);
on_sep5=0;on_x5=0;on_o5=0;on_a5=0;

on_sep5=on_sep5_ref;on_x5=on_x5_ref;on_o5=on_o5_ref;on_a5=on_a5_ref;
scale0=1;
value,acbyhs4.l5b1_on_o5   / limitMCBY*100*on_o5;
value,acbch6.l5b1_on_o5    / limitMCBC*100*on_o5;
value,acbyhs4.r5b1_on_o5   / limitMCBY*100*on_o5;
value,acbch5.r5b1_on_o5    / limitMCBC*100*on_o5;
value,acbyhs4.l5b2_on_o5   / limitMCBY*100*on_o5;
value,acbch5.l5b2_on_o5    / limitMCBC*100*on_o5;
value,acbyhs4.r5b2_on_o5   / limitMCBY*100*on_o5;
value,acbch6.r5b2_on_o5    / limitMCBC*100*on_o5;
value,acbyhs4.l5b1_on_x5   / limitMCBY*100*on_x5;
value,acbch6.l5b1_on_x5    / limitMCBC*100*on_x5;
value,acbyhs4.r5b1_on_x5   / limitMCBY*100*on_x5;
value,acbch5.r5b1_on_x5    / limitMCBC*100*on_x5;
value,acbyhs4.l5b2_on_x5   / limitMCBY*100*on_x5;
value,acbch5.l5b2_on_x5    / limitMCBC*100*on_x5;
value,acbyhs4.r5b2_on_x5   / limitMCBY*100*on_x5;
value,acbch6.r5b2_on_x5    / limitMCBC*100*on_x5;
value,acbcv5.l5b1_on_sep5  / limitMCBC*100*on_sep5;
value,acbyvs4.l5b1_on_sep5 / limitMCBY*100*on_sep5;
value,acbyvs4.r5b1_on_sep5 / limitMCBY*100*on_sep5;
value,acbcv6.r5b1_on_sep5  / limitMCBC*100*on_sep5;
value,acbyvs4.l5b2_on_sep5 / limitMCBY*100*on_sep5;
value,acbcv6.l5b2_on_sep5  / limitMCBC*100*on_sep5;
value,acbyvs4.r5b2_on_sep5 / limitMCBY*100*on_sep5;
value,acbcv5.r5b2_on_sep5  / limitMCBC*100*on_sep5;

on_sep5=on_sep5_aux;on_x5=on_x5_aux;on_o5=on_o5_aux;on_a5=on_a5_aux;

value,tarir5_o5,tarir5_x5,tarir5_sep5;


tarir5_cross=tarir5_sep5+tarir5_x5+tarir5_o5;
